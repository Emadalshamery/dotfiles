# This is mostly based on:
# <http://stevelosh.com/blog/2010/02/my-extravagant-zsh-prompt/>
#
# For help on how prompts work, read:
# <http://www.acm.uiuc.edu/workshops/zsh/prompt/escapes.html>
#
# For help on how colors work, read:
#
# * <https://github.com/zsh-users/zsh/blob/master/Functions/Misc/colors>
# * <https://wiki.archlinux.org/index.php/zsh#Customizing_the_prompt>
# * <https://wiki.archlinux.org/index.php/zsh#Colors>

function Color {
  echo -n "%{$fg[$1]%}$2%{$reset_color%}"
}

function Color__red {
  echo "$(Color red "$1")"
}

function Color__green {
  echo "$(Color green "$1")"
}

function Color__yellow {
  echo "$(Color yellow "$1")"
}

function Color__blue {
  echo "$(Color blue "$1")"
}

function Color__magenta {
  echo "$(Color magenta "$1")"
}

function Color__cyan {
  echo "$(Color cyan "$1")"
}

function Color__gray {
  echo "$(Color black "$1")"
}

# Source: <https://stackoverflow.com/questions/1527049/join-elements-of-an-array>
function join_by {
  local d=$1
  shift
  echo -n "$1"
  shift
  printf "%s" "${@/#/$d}"
}

function Prompt__Fragment__collapsed_pwd {
  echo "${PWD/#$HOME/~}"
}

function Prompt__Fragment__time {
  echo -n '%D{%-I:%M:%S %p}'
}

function Prompt__Fragment__username {
  echo -n '%n'
}

function Prompt__Fragment__host {
  echo -n '%m'
}

function Prompt__Fragment__git_branch {
  if [[ -e .git ]]; then
    local ref=""

    ref="$(git symbolic-ref HEAD 2>/dev/null)"

    if [[ -z $ref ]]; then
      ref="$(git rev-parse --short HEAD 2>/dev/null)"
    fi

    # remove "refs/heads/" from beginning of $ref
    echo "${ref#refs/heads/}"
  fi
}

function Prompt__Fragment__ruby_version {
  if [[ -e .ruby-version || -e Gemfile ]]; then
    local out="$(rbenv version | sed -e 's/ (set.*$//')"

    local gemsets="$(rbenv gemset active 2>/dev/null | perl -ne 'print join(", ", split(" ")) . "\n"')"
    if [[ -n $gemsets ]]; then
      out+=" (gemsets: $gemsets)"
    fi

    echo -n "$out"
  fi
}

function Prompt__Fragment__python_version {
  if [[ -e .python-version || -e Pipfile ]]; then
    local out="$(pyenv version | sed -e 's/ (set.*$//')"

    echo -n "$out"
  fi
}

function Prompt__Fragment__node_version {
  if [[ -e .nvmrc || -e node_modules ]]; then
    local out="$(nvm version | sed -e 's/^v//')"

    echo -n "$out"
  fi
}

function Prompt__rbenv_path {
  echo -n "$(which rbenv &>/dev/null)"
}

function Prompt__time {
  echo -n "($(Color__blue "$(Prompt__Fragment__time)")) "
}

function Prompt__username_fragment {
  echo -n "$(Color__blue "$(Prompt__Fragment__username)")"
}

function Prompt__git_branch_fragment {
  local git_branch="$(Prompt__Fragment__git_branch)"

  if [[ -n $git_branch ]]; then
    echo -n " on $(Color__yellow "$git_branch")"
  fi
}

function Prompt__language_versions_fragment {
  local ruby_version="$(Prompt__Fragment__ruby_version)"
  local python_version="$(Prompt__Fragment__python_version)"
  local node_version="$(Prompt__Fragment__node_version)"

  pieces=()

  if [[ -n $ruby_version ]]; then
    pieces+="$(Color__red "ruby $ruby_version")"
  fi

  if [[ -n $python_version ]]; then
    pieces+="$(Color__red "python $python_version")"
  fi

  if [[ -n $node_version ]]; then
    pieces+="$(Color__red "node $node_version")"
  fi

  if [[ ${#pieces} -gt 0 ]]; then
    echo -n " using $(join_by ", " "${pieces[@]}")"
  fi
}

function Prompt__cwd_fragment {
  echo -n "$(Color__green "$(Prompt__Fragment__collapsed_pwd)")"
}

function Prompt__rc_char_fragment {
  echo -n "$(Prompt__Fragment__rc_char "$1") "
}

function Prompt__value {
  echo -n '$(Prompt__time)'
  echo -n '$(Prompt__cwd_fragment)'
  echo -n '$(Prompt__git_branch_fragment)'
  echo -n '$(Prompt__language_versions_fragment)'
  echo -n "\n"
  echo 'âž¤  '
}

function Prompt__set {
  PROMPT="$(Prompt__value)"
}

autoload -Uz colors && colors

Prompt__set

# vi: ft=sh
